plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 22
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
        }
    }
    publishing {
        singleVariant("release") {
//            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    implementation files('libs/android-support-v4.jar')
}

tasks.register("javadocEmptyJar", org.gradle.jvm.tasks.Jar) {
    archiveClassifier = "javadoc"
}

group = 'io.github.wasnot'
version = "0.0.2"

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = group
                artifactId = "loopingviewpager"
                version = version
                from(components["release"])
                artifact(javadocEmptyJar)

                pom {
                    name = "LoopingViewPager for Android"
                    description = "LoopingViewPager for Android"
                    url = "https://github.com/wasnot/LoopingViewPager"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "wasnot"
                            name = "wasnot"
                            email = "wasnot.apps@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/wasnot/LoopingViewPager.git\""
                        developerConnection = "scm:git:ssh://github.com/wasnot/LoopingViewPager.git"
                        url = "https://github.com/wasnot/LoopingViewPager"
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('repos')
            }
        }
    }
    signing {
        sign(publishing.publications.maven)
    }
}

tasks.register("makeArchive", Zip) {
    dependsOn("publishMavenPublicationToMavenRepository")
    from(layout.buildDirectory.dir("repos/io/github/wasnot/loopingviewpager/$version"))
    into("io/github/wasnot/loopingviewpager/$version")
}